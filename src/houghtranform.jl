
#function to compute local maximum lines with values > threshold and return a vector containing them
function findlocalmaxima(accumulator_matrix::Array{Integer,2},threshold::Integer)
    validLines = Vector{CartesianIndex}(0)
    for val in CartesianRange(size(accumulator_matrix))
        if  accumulator_matrix[val] > threshold &&
            accumulator_matrix[val] > accumulator_matrix[val[1],val[2] - 1] &&
            accumulator_matrix[val] >= accumulator_matrix[val[1],val[2] + 1] &&
            accumulator_matrix[val] > accumulator_matrix[val[1] - 1,val[2]] &&
            accumulator_matrix[val] >= accumulator_matrix[val[1] + 1,val[2]]
            push!(validLines,val)
        end
    end
    validLines
end

"""
```
lines = hough_transform_standard(image, rho, theta, min_theta, max_theta, threshold, linesMax)
```

Returns an vector of tuples corresponding to the tuples of (r,t) where r and t are parameters for normal form of line:
    x*cos(t) + y*sin(t) = r

r = length of perpendicular from (0,0) to the line
t = angle between perpendicular from (0,0) to the line and axis

The lines are generated by applying hough transform on the image.

Parameters:
    image       = image to apply hough transform on
    rho         = discrete steps for radius
    theta       = discrete steps for theta
    min_theta   = minimum theta of lines
    max_theta   = maximum theta of lines
    threshold   = no of points to pass through line for considering it valid
    linesMax    = maximum no of lines to return

"""

function hough_transform_standard{T<:Union{Bool,Gray{Bool}}}(
            img::AbstractArray{T,2},
            rho::Number, theta::Number,
            min_theta::Number, max_theta::Number,
            threshold::Integer, linesMax::Integer)
   
    theta > 0 || error("theta threshold must be positive")
    rho > 0 || error("radius threshold must be positive")
    min_theta < max_theta || error("max_theta must be greater than min_theta")

    width = size(img)[2]
    height = size(img)[1]
    irho = 1 / rho;
    numangle::Int = round((max_theta - min_theta)/theta)
    numrho::Int = round(((width + height)*2 + 1)/rho)

    accumulator_matrix = Array{Integer}(numangle + 2, numrho + 2)
    fill!(accumulator_matrix, 0)

    #Pre-Computed sines and cosines in tables
    tabsin = Vector{Number}(numangle)
    tabcos = Vector{Number}(numangle)
    for i in 1:numangle
        tabsin[i] = (sin(min_theta + (i-1)*theta) * irho)
        tabcos[i] = (cos(min_theta + (i-1)*theta) * irho)
    end


    #Hough Transform implementation
    constadd::Int = round((numrho -1)/2)
    for pix in CartesianRange(size(img))
        if img[pix]
            for i in 1:numangle
                dist::Int = round((pix[1] * tabsin[i] + pix[2] * tabcos[i]))
                dist += constadd
                accumulator_matrix[i + 1, dist + 1] += 1
            end
        end
    end

    #Finding local maximum lines
    validLines = findlocalmaxima(accumulator_matrix, threshold)

    #Sorting by value in accumulator_matrix
    sort!(validLines, by = (x)->accumulator_matrix[x], rev = true)

    linesMax = min(linesMax, length(validLines))

    lines = Vector{Tuple{Number,Number}}(0)

    #Getting lines with Maximum value in accumulator_matrix && size(lines) < linesMax
    for l in 1:linesMax
        lrho = ((validLines[l][2]-1) - (numrho - 1)*0.5)*rho
        langle = ((validLines[l][1]-1)*theta + min_theta)
        push!(lines,(lrho,langle))
    end

    lines

end


